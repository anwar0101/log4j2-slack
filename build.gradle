plugins {
    id 'ru.vyarus.pom' version '1.2.0'
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'java'

group projectGroupId
version projectVersion

sourceCompatibility = javaVersionNumber

ext {
    isReleaseVersion = !(projectVersion =~ /-SNAPSHOT$/)
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.apache.logging.log4j:log4j-core:$log4j2Version"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile("com.github.seratch:jslack:$jslackVersion") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
    group 'build'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
    extension 'jar'
    group 'build'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

pom {
    name 'Log4j2 Slack Appender'
    description 'This is a highly customizable appender for Apache SLF4J2 to push the logs to Slack using webhooks'
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/rage28/log4j2-slack'
        connection 'scm:git@github.com:rage28/log4j2-slack.git'
        developerConnection 'scm:git@github.com:rage28/log4j2-slack.git'
    }
    developers {
        developer {
            id "rage28"
            name "Raghavendra Bhuvan"
            email "rage28@gmail.com"
            roles {
                role 'Developer'
            }
        }
    }
}

publishing {
    repositories {
        mavenLocal()
        jcenter()
    }
    publications {
        jar(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact javadocJar

            groupId projectGroupId
            artifactId rootProject.name
            version projectVersion
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApikey"
    publications = ['jar']

    pkg {
        repo = 'java'
        name = 'log4j2-slack'
        desc = 'A highly customizable appender for Apache SLF4J2 to push the logs to Slack using webhooks'

        websiteUrl = 'https://github.com/rage28/log4j2-slack'
        issueTrackerUrl = 'https://github.com/rage28/log4j2-slack/issues'
        vcsUrl = 'https://github.com/rage28/log4j2-slack.git'

        licenses = ['Apache-2.0']
        labels = ['log4j2', 'slack', 'slf4j']

        publicDownloadNumbers = true

        githubRepo = 'rage28/log4j2-slack'
        githubReleaseNotesFile = 'README.md'

        version {
            name = "$projectVersion"
            vcsTag = "$projectVersion"
            released = new Date()

            gpg {
                sign = true
                passphrase = "$bintrayGpgPassword"
            }

            mavenCentralSync {
                sync = false
                user = "$ossrhUsername"
                password = "$ossrhPassword"
                close = '1'
            }
        }
    }
}